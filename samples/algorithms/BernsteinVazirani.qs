/// # Sample
/// Bernstein-Vazirani algorithm
///
/// # Description
/// The Bernstein-Vazirani algorithm determines a bit string encoded in a
/// function.
///
/// This Q# program implements the Bernstein-Vazirani algorithm.
namespace Sample {
    open Microsoft.Quantum.Arrays;
    open Microsoft.Quantum.Convert;
    open Microsoft.Quantum.Diagnostics;
    open Microsoft.Quantum.Math;
    open Microsoft.Quantum.Measurement;

    /// # Summary
    /// `LearnParityViaFourierSampling` implements the Bernstein-Vazirani
    /// quantum algorithm. This algorithm computes for a given Boolean function
    /// that is promised to be a parity ğ‘“(ğ‘¥â‚€, â€¦, ğ‘¥â‚™â‚‹â‚) = Î£áµ¢ ğ‘Ÿáµ¢ ğ‘¥áµ¢ a result in the
    /// form of a bit vector (ğ‘Ÿâ‚€, â€¦, ğ‘Ÿâ‚™â‚‹â‚) corresponding to the parity function.
    /// Note that it is promised that the function is actually a parity
    /// function.
    ///
    /// # Input
    /// ## Uf
    /// A quantum operation that implements |ğ‘¥âŒª|ğ‘¦âŒª â†¦ |ğ‘¥âŒª|ğ‘¦ âŠ• ğ‘“(ğ‘¥)âŒª,
    /// where ğ‘“ is a Boolean function that implements a parity Î£áµ¢ ğ‘Ÿáµ¢ ğ‘¥áµ¢.
    /// ## n
    /// The number of bits in the input register |ğ‘¥âŒª.
    ///
    /// # Output
    /// An array of type `Result[]` that contains the parity ğ‘Ÿâƒ— = (ğ‘Ÿâ‚€, â€¦, ğ‘Ÿâ‚™â‚‹â‚).
    ///
    /// # See Also
    /// - For details see Section 1.4.3 of Nielsen & Chuang.
    ///
    /// # References
    /// - [ *Ethan Bernstein and Umesh Vazirani*,
    ///     SIAM J. Comput., 26(5), 1411â€“1473, 1997 ]
    ///   (https://doi.org/10.1137/S0097539796300921)
    operation LearnParityViaFourierSampling(
        Uf: ((Qubit[], Qubit) => Unit),
        n : Int) : Result[] {

        // We allocate n + 1 clean qubits. Note that the function Uf is defined
        // on inputs of the form (x, y), where x has n bits and y has 1 bit.
        use queryRegister = Qubit[n];
        use target = Qubit();

        // The last qubit needs to be flipped so that the function will actually
        // be computed into the phase when Uf is applied.
        X(target);

        within {
            // Now, a Hadamard transform is applied to each of the qubits. As
            // the last step before the measurement, a Hadamard transform is
            // applied to all qubits except last one. We could apply the
            // transform to the last qubit also, but this would not affect the
            // final outcome.
            // We use a within-apply block to ensure that the Hadamard transform
            // is correctly inverted.
            ApplyToEachA(H, queryRegister);
        } apply {
            H(target);
            // We now apply Uf to the n+1 qubits, computing
            // |x, yâŒª â†¦ |x, y âŠ• f(x)âŒª.
            Uf(queryRegister, target);
        }

        // Measure all qubits and reset them to the |0âŒª state so that they can
        // be safely deallocated at the end of the block.
        let resultArray = ForEach(MResetZ, queryRegister);

        // Finally, the last qubit, which held the y-register, is reset.
        Reset(target);

        // The result is already contained in resultArray so no further
        // post-processing is necessary.
        return resultArray;
    }

    // TODO: Move these comments to a place where they fit better.
    // To demonstrate the Bernsteinâ€“Vazirani algorithm, we define
    // a function which returns black-box operations (Qubit[] => ()) of
    // the form U_f |ğ‘¥âŒª|ğ‘¦âŒª = |ğ‘¥âŒª|ğ‘¦ âŠ• ğ‘“(ğ‘¥)âŒª, as described above.

    // In particular, we define ğ‘“ by providing the pattern ğ‘Ÿâƒ—. Thus,
    // we can easily assert that the pattern measured by the
    // Bernsteinâ€“Vazirani algorithm matches the pattern we used
    // to define ğ‘“.

    /// # Summary
    /// Given an integer pattern that can be represented as a bitstring
    /// ğ‘Ÿâƒ— = (râ‚€, â€¦, râ‚™â‚‹â‚), this operation applies a unitary ğ‘ˆ that acts on ğ‘› + 1
    /// qubits as:
    ///     ğ‘ˆ |ğ‘¥âŒª|ğ‘¦âŒª = |ğ‘¥âŒª|ğ‘¦ âŠ• ğ‘“(ğ‘¥)âŒª
    /// Where ğ‘“(ğ‘¥) = Î£áµ¢ ğ‘¥áµ¢ ğ‘Ÿáµ¢ mod 2.
    ///
    /// # Input
    /// ## pattern
    /// The integer pattern that can be represented as a bitstring ğ‘Ÿâƒ— used to
    /// define the function ğ‘“.
    /// ## xRegister
    /// Represents the |ğ‘¥âŒª register that ğ‘ˆ acts on.
    /// ## yQubit
    /// Represents the |ğ‘¦âŒª qubit that ğ‘ˆ acts on.
    internal operation ApplyParityOperation(
        pattern: Int,
        xRegister: Qubit[],
        yQubit: Qubit) : Unit {

        // `xRegister` muts have enough qubits to represent the pattern.
        let requiredBits = BitSizeI(pattern);
        let availableQubits = Length(xRegister);
        Fact(
            availableQubits >= requiredBits,
            $"Pattern {pattern} requires {requiredBits} bits to be " +
            $"represented but quantum register only has " +
            $"{availableQubits} qubits");

        // Apply the quantum operations that encode the pattern.
        for index in IndexRange(xRegister) {
            if ((pattern &&& 2^index) != 0) {
                CNOT(xRegister[index], yQubit);
            }
        }
    }

    internal operation EncodePatternInParityOperation(pattern: Int) :
        (Qubit[], Qubit) => Unit {
        return ApplyParityOperation(pattern, _, _);
    }

    // For convenience, we provide an operation that converts a result array
    // into an integer.
    operation RunBernsteinVazirani(
        nQubits: Int,
        Uf : ((Qubit[], Qubit) => Unit)): Int {
        let result = LearnParityViaFourierSampling(Uf, nQubits);
        return ResultArrayAsInt(result);
    }

    @EntryPoint()
    operation Main() : Unit {
        let nQubits = 12;
        // TODO: Consider whether these comments belong here.
        // Parity Sampling with the Bernsteinâ€“Vazirani Algorithm:

        // Consider a function ğ‘“(ğ‘¥âƒ—) on bitstrings ğ‘¥âƒ— = (ğ‘¥â‚€, â€¦, ğ‘¥â‚™â‚‹â‚)
        // of the form
        //
        //     ğ‘“(ğ‘¥âƒ—) â‰” Î£áµ¢ ğ‘¥áµ¢ ğ‘Ÿáµ¢
        //
        // where ğ‘Ÿâƒ— = (ğ‘Ÿâ‚€, â€¦, ğ‘Ÿâ‚™â‚‹â‚) is an unknown bitstring that
        // determines the parity of ğ‘“.

        // The Bernsteinâ€“Vazirani algorithm allows determining ğ‘Ÿ given a
        // quantum operation that implements
        //
        //     |ğ‘¥âŒª|ğ‘¦âŒª â†¦ |ğ‘¥âŒª|ğ‘¦ âŠ• ğ‘“(ğ‘¥)âŒª.
        //
        // In SimpleAlgorithms.qs, we implement this algorithm as the
        // operation RunBernsteinVazirani. This operation takes an
        // integer whose bits describe ğ‘Ÿ, then uses those bits to
        // construct an appropriate operation, and finally measures ğ‘Ÿ.

        // We call that operation here, ensuring that we always get the
        // same value for ğ‘Ÿ that we provided as input.

        let patterns = [238, 3435];
        for pattern in patterns {
            let encodingOperation = EncodePatternInParityOperation(pattern);
            let decodedPattern = RunBernsteinVazirani(
                nQubits, encodingOperation);
            Fact(
                decodedPattern == pattern,
                $"Decoded pattern {decodedPattern}, but expected {pattern}.");

            Message($"Successfully decoded pattern: {decodedPattern}");
        }
    }

}

/// # Sample
/// Bernstein-Vazirani algorithm
///
/// # Description
/// The Bernstein-Vazirani algorithm determines the value of a bit string
/// encoded in a function.
///
/// This Q# program implements the Bernstein-Vazirani algorithm.
namespace Sample {
    open Microsoft.Quantum.Arrays;
    open Microsoft.Quantum.Convert;
    open Microsoft.Quantum.Diagnostics;
    open Microsoft.Quantum.Math;
    open Microsoft.Quantum.Measurement;

    @EntryPoint()
    operation Main() : Result[] {
        // Consider a function ð‘“(ð‘¥âƒ—) on bitstrings ð‘¥âƒ— = (ð‘¥â‚€, â€¦, ð‘¥â‚™â‚‹â‚) of the form
        //     ð‘“(ð‘¥âƒ—) â‰” Î£áµ¢ ð‘¥áµ¢ ð‘Ÿáµ¢
        // where ð‘Ÿâƒ— = (ð‘Ÿâ‚€, â€¦, ð‘Ÿâ‚™â‚‹â‚) is an unknown bit string that determines the
        // parity of ð‘“.

        // The Bernsteinâ€“Vazirani algorithm allows determining ð‘Ÿ given a
        // quantum operation that implements
        //     |ð‘¥âŒª|ð‘¦âŒª â†¦ |ð‘¥âŒª|ð‘¦ âŠ• ð‘“(ð‘¥)âŒª.

        // This entry point function of this program, `Main`, shows how to use
        // the `BernsteinVazirani` operation to determine the value of bitstring
        // ð‘Ÿ.
        let secretBitString = SecretBitStringAsBoolArray();
        let parityOperation = EncodeBitStringAsParityOperation(secretBitString);
        let decodedBitString = BernsteinVazirani(
            parityOperation, Length(secretBitString));

        return decodedBitString;
    }

    /// # Summary
    /// This operation implements the Bernstein-Vazirani quantum algorithm.
    /// This algorithm computes for a given Boolean function that is promised to
    /// be a parity ð‘“(ð‘¥â‚€, â€¦, ð‘¥â‚™â‚‹â‚) = Î£áµ¢ ð‘Ÿáµ¢ ð‘¥áµ¢ a result in the form of a bit
    /// vector (ð‘Ÿâ‚€, â€¦, ð‘Ÿâ‚™â‚‹â‚) corresponding to the parity function.
    /// Note that it is promised that the function is actually a parity
    /// function.
    ///
    /// # Input
    /// ## Uf
    /// A quantum operation that implements |ð‘¥âŒª|ð‘¦âŒª â†¦ |ð‘¥âŒª|ð‘¦ âŠ• ð‘“(ð‘¥)âŒª,
    /// where ð‘“ is a Boolean function that implements a parity Î£áµ¢ ð‘Ÿáµ¢ ð‘¥áµ¢.
    /// ## n
    /// The number of bits in the input register |ð‘¥âŒª.
    ///
    /// # Output
    /// An array of type `Result[]` that contains the parity ð‘Ÿâƒ— = (ð‘Ÿâ‚€, â€¦, ð‘Ÿâ‚™â‚‹â‚).
    ///
    /// # See Also
    /// - For details see Section 1.4.3 of Nielsen & Chuang.
    ///
    /// # References
    /// - [ *Ethan Bernstein and Umesh Vazirani*,
    ///     SIAM J. Comput., 26(5), 1411â€“1473, 1997 ]
    ///   (https://doi.org/10.1137/S0097539796300921)
    operation BernsteinVazirani(Uf : ((Qubit[], Qubit) => Unit), n : Int)
    : Result[] {
        // We allocate n + 1 clean qubits. Note that the function parameter Uf is defined
        // on inputs of the form (x, y), where x has n bits and y has 1 bit.
        use queryRegister = Qubit[n];
        use target = Qubit();

        // The last qubit needs to be flipped so that a relative phase is
        // introduced when we apply a Hadamard gate later on and we can use
        // phase kickback when Uf is applied.
        X(target);

        within {
            // Now, a Hadamard transform is applied to each of the qubits. As
            // the last step before the measurement, a Hadamard transform is
            // applied to all qubits except the last one. We could also
            // transform the last qubit, but this would not affect the
            // final outcome.
            // We use a within-apply block to ensure that the Hadamard transform
            // is correctly inverted.
            ApplyToEachA(H, queryRegister);
        } apply {
            H(target);
            // We now apply Uf to the n+1 qubits, computing
            // |x, yâŒª â†¦ |x, y âŠ• f(x)âŒª.
            Uf(queryRegister, target);
        }

        // Measure all qubits and reset them to the |0âŒª state so that they can
        // be safely deallocated at the end of the block.
        let resultArray = ForEach(MResetZ, queryRegister);

        // Finally, the last qubit, which held the y-register, is reset.
        Reset(target);

        // The result is already contained in resultArray so no further
        // post-processing is necessary.
        return resultArray;
    }

    /// # Summary
    /// Given bit string ð‘Ÿâƒ— = (râ‚€, â€¦, râ‚™â‚‹â‚), represented as an array of Booleans,
    /// this operation applies a unitary ð‘ˆ that acts on ð‘› + 1 qubits as:
    ///     ð‘ˆ |ð‘¥âŒª|ð‘¦âŒª = |ð‘¥âŒª|ð‘¦ âŠ• ð‘“(ð‘¥)âŒª
    /// where ð‘“(ð‘¥) = Î£áµ¢ ð‘¥áµ¢ ð‘Ÿáµ¢ mod 2.
    ///
    /// # Input
    /// ## bitStringAsBoolArray
    /// A bit string ð‘Ÿâƒ—, represented as an array of Booleans, used to define the
    /// function ð‘“.
    /// ## xRegister
    /// Represents the |ð‘¥âŒª register that ð‘ˆ acts on.
    /// ## yQubit
    /// Represents the |ð‘¦âŒª qubit that ð‘ˆ acts on.
    operation ApplyParityOperation(
        bitStringAsBoolArray : Bool[],
        xRegister : Qubit[],
        yQubit : Qubit)
    : Unit {
        // `xRegister` muts have enough qubits to represent the integer.
        let requiredBits = Length(bitStringAsBoolArray);
        let availableQubits = Length(xRegister);
        Fact(
            availableQubits >= requiredBits,
            $"The bitstring has {requiredBits} bits but the quantum register " +
            $"only has {availableQubits} qubits");

        // Apply the quantum operations that encode the bit string.
        for (index, bit) in Enumerated(bitStringAsBoolArray) {
            if bit {
                CNOT(xRegister[index], yQubit);
            }
        }
    }

    /// # Summary
    /// This is a higher-order operation which returns an operation (Qubit[], Qubit) => () of the form
    /// U_f |ð‘¥âŒª|ð‘¦âŒª = |ð‘¥âŒª|ð‘¦ âŠ• ð‘“(ð‘¥)âŒª.
    /// We define ð‘“ by providing the bit string ð‘Ÿâƒ— as an integer.
    operation EncodeBitStringAsParityOperation(bitStringAsBoolArray : Bool[])
    : (Qubit[], Qubit) => Unit {
        return ApplyParityOperation(bitStringAsBoolArray, _, _);
    }

    /// # Summary
    /// Returns a particular bit string as an array of Booleans.
    function SecretBitStringAsBoolArray() : Bool[] {
        return [true, false, true, false, true];
    }
}
